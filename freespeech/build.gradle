apply plugin: 'com.android.application'

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'com.blogspot.tonyatkins:recorder:2.0.1@aar'
    compile 'com.blogspot.tonyatkins:picker:2.0.1@aar'
    compile 'com.android.support:support-annotations:21.0.3'
}

// Required by maven plugin
group = "com.blogspot.tonyatkins"

android {
    // TODO: Fix all lint errors and remove this
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 16
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId    "com.blogspot.tonyatkins.freespeech"
        minSdkVersion    11
        targetSdkVersion 11
        versionName      project.property("version")

        // Required to replace variables in AndroidManifest.xml with our settings
        manifestPlaceholders = [
            versionName: project.property("version"),
            versionCode: project.property("versionCode")
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Free Speech for Android'
            packaging 'apk'
            description 'Free Speech for Android is a free and open source picture board to help people who have problems communicating.'
            url 'https://bitbucket.org/duhrer/free-speech-for-android'

            scm {
                url 'https://duhrer@bitbucket.org/duhrer/free-speech-for-android'
                connection 'scm:hg:https://duhrer@bitbucket.org/duhrer/free-speech-for-android'
                developerConnection 'scm:hg:https://duhrer@bitbucket.org/duhrer/free-speech-for-android'
            }

            licenses {
                license {
                    name 'BSD'
                    url 'http://www.freebsd.org/copyright/freebsd-license.html'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'duhrer'
                    name 'Tony Atkins'
                    email 'duhrer@gmail.com'
                }
            }
        }
    }
}